import groovy.json.JsonOutput
import groovy.json.JsonSlurper

java {
    toolchain.languageVersion = JavaLanguageVersion.of(25)
}

def os = org.gradle.internal.os.OperatingSystem.current()

ext {
    if (os.isWindows()) {
        hytaleHome = "${System.getenv("APPDATA")}/Hytale"
    } else if (os.isMacOsX()) {
        hytaleHome = "${System.getProperty("user.home")}/Library/Application Support/Hytale"
    } else {
        def xdgDataHome = System.getenv("XDG_DATA_HOME")
        if (xdgDataHome != null && !xdgDataHome.isBlank()) {
            hytaleHome = "${xdgDataHome}/Hytale"
        } else {
            hytaleHome = "${System.getProperty("user.home")}/.local/share/Hytale"
        }
    }
}

dependencies {
    implementation project(':common')
    compileOnly (files("$hytaleHome/install/release/package/game/latest/Server/HytaleServer.jar"))
}

// Updates the manifest.json version
tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestFile.text = JsonOutput.prettyPrint(JsonOutput.toJson(manifestJson))
    }
}

tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}